plugins {
    id 'java'
    id 'jacoco'
    id 'net.researchgate.release' version '2.6.0'
}

group 'com.thoughtworks.com'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'com.thoughtworks.gocd',
        version    : project.version,
        goCdVersion: '19.8.0',
        name       : 'Git Path Material',
        description: 'Git Path Material plugin for GoCD',
        vendorName : 'GoCd Contributors',
        vendorUrl  : 'https://github.com/TWChennai/gocd-git-path-material-plugin'
]

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

check.dependsOn jacocoTestReport

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.10'
    testCompileOnly 'org.projectlombok:lombok:1.16.10'
    annotationProcessor 'org.projectlombok:lombok:1.16.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.16.10'

    implementation "cd.go.plugin:go-plugin-api:${project.pluginDesc.goCdVersion}"

    implementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    implementation 'commons-validator:commons-validator:1.4.0'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r'
    implementation 'joda-time:joda-time:2.3'
    implementation 'commons-io:commons-io:2.4'
    implementation 'ch.qos.logback:logback-classic:1.1.7'
    implementation 'org.apache.commons:commons-lang3:3.4'

    testImplementation 'junit:junit:4.11'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.2'
    testImplementation 'org.powermock:powermock-core:1.6.2'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.2'
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

task startGoCd(dependsOn: build, type: Exec) {
    dependsOn build
    environment("PLUGIN_VERSION", "$version")
    commandLine "docker-compose", "up", "-d"
}

task restartGoCd(dependsOn: build, type: Exec) {
    dependsOn build
    environment("PLUGIN_VERSION", "$version")
    commandLine "docker-compose", "restart"
}

task stopGoCd(type: Exec) {
    environment("PLUGIN_VERSION", "$version")
    commandLine "docker-compose", "stop"
}
