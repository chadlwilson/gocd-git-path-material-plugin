plugins {
    id 'java'
    id 'jacoco'
    id 'io.franzbecker.gradle-lombok' version '3.1.0'
    id 'net.researchgate.release' version '2.8.1'
}

group 'com.thoughtworks.com'

sourceCompatibility = 1.9
targetCompatibility = 1.9

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'com.thoughtworks.gocd',
        version    : project.version,
        goCdVersion: '18.10.0',
        name       : 'Git Path Material',
        description: 'Git Path Material plugin for GoCD',
        vendorName : 'GoCd Contributors',
        vendorUrl  : 'https://github.com/chadlwilson/gocd-git-path-material-plugin'
]

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

dependencies {
    implementation "cd.go.plugin:go-plugin-api:${project.pluginDesc.goCdVersion}"

    implementation 'in.ashwanthkumar:git-cmd:2.0.0-rc1'
    implementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    implementation 'org.apache.commons:commons-lang3:3.9'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-core:2.1'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-core:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

task startGoCd(dependsOn: build, type: Exec) {
    environment("VERSION", "$version")
    commandLine "docker-compose", "up", "-d"
}

task restartGoCd(dependsOn: build, type: Exec) {
    environment("VERSION", "$version")
    commandLine "docker-compose", "restart"
}

task stopGoCd(type: Exec) {
    environment("VERSION", "$version")
    commandLine "docker-compose", "stop"
}